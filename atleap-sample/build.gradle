/*
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

*/

apply plugin: 'com.android.application'

/*
repositories {
    mavenCentral()
    //uncomment these lines if you would like to have access to latest snapshots at OSS Sonatype
    //  maven {
    //        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    //  }

}
*/

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode new Date().time.toString()[0..9].toInteger()
        versionName "0.1." + new Date().format('yyyyMMddHHmmss')
        applicationId "com.blandware.android.atleap.sample"
    }

    dexOptions {
        // Skip pre-dexing because in Travis that causes OOM and java exits with 137 status
        javaMaxHeapSize "2048M"
        incremental true
        preDexLibraries = false
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

}


configurations {
    compile.exclude group: 'org.apache.commons', module: 'commons-io'

}


dependencies {
    compile project(':atleap-core')
    //comment above line and uncomment below one in order to compile this app independently
    //compile 'com.blandware.android.atleap:core:0.4-SNAPSHOT@aar'

    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    
    compile 'com.octo.android.robospice:robospice:1.4.14'
    compile 'com.octo.android.robospice:robospice-cache:1.4.14'
    compile 'com.octo.android.robospice:robospice-ormlite:1.4.14'
    compile 'com.octo.android.robospice:robospice-okhttp:1.4.14'
    compile 'com.octo.android.robospice:robospice-retrofit:1.4.14'
    compile 'commons-io:commons-io:1.3.2'

    compile 'com.squareup.retrofit:converter-jackson:1.6.1'

    compile 'com.squareup.okhttp:okhttp:2.7.1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.7.1'
    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

}

//in order to activate signature please make the following steps:
// 1. add the following line into file ~/.gradle/gradle.properties
//      com.blandware.android.atleap.sample.properties=[path_to_signature_settings]
// 2. create the file [path_to_signature_settings] with the following content
//      store.file=[path_to_store_with_private_key]
//      store.password=[password_to_store]
//      key.alias=[alias_of_private_key]
//      key.password=[password_to_private_key]

if (project.hasProperty("com.blandware.android.atleap.sample.properties")
        && new File(project.property("com.blandware.android.atleap.sample.properties")).exists()) {

    Properties props = new Properties()
    props.load(new FileInputStream(file(project.property("com.blandware.android.atleap.sample.properties"))))

    android {
        signingConfigs {
            release {
                storeFile file(props['store.file'])
                storePassword props['store.password']
                keyAlias props['key.alias']
                keyPassword props['key.password']
            }

            buildTypes {
                release {
                    signingConfig signingConfigs.release
                }
            }
        }
    }
}
